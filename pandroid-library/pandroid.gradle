dependencies {
    annotationProcessor "com.leroymerlin.pandroid:pandroid-compiler:$pandroidVersion"
    compile "com.leroymerlin.pandroid:pandroid-library:$pandroidVersion"
    provided "com.leroymerlin.pandroid:pandroid-annotations:$pandroidVersion"
}

plugins.withId('com.leroymerlin.android-templates') {
    dependencies {
        template "com.leroymerlin.pandroid:pandroid-library:$pandroidVersion:templates"
    }
}

//tag::Proguard[]
android {

    lintOptions {
        disable 'InvalidPackage'
    }

    buildTypes {
        all {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'build/pandroid/pandroid-proguard-rules.pro'
            if (file('proguard-rules.pro').exists()) {
                proguardFiles 'proguard-rules.pro'
            }
        }
        if (!plugins.hasPlugin("com.android.library")) {
            release {
                minifyEnabled true
            }
        }
    }

}

//end::Proguard[]


pandroid {

    configurators {
        "io.reactivex.rxjava2:rxandroid" { //RX
            gradle {
                dependencies {
                    compile "io.reactivex.rxjava2:rxandroid:$libraryRxAndroidVersion"
                    compile "io.reactivex.rxjava2:rxjava:$libraryRxJavaVersion"
                }
            }
        }

        "com.jakewharton:butterknife" { // ButterknifeLifecycleDelegate
            gradle {
                dependencies {
                    compile "com.jakewharton:butterknife:$libraryButterknifeVersion"
                    annotationProcessor "com.jakewharton:butterknife-compiler:$libraryButterknifeVersion"

                }
            }
        }
        "com.github.frankiesardo:icepick" { // IcepickLifecycleDelegate
            gradle {
                repositories {
                    maven { url "https://clojars.org/repo/" }
                }
                dependencies {
                    compile "frankiesardo:icepick:$libraryIcepickVersion"
                    annotationProcessor "frankiesardo:icepick-processor:$libraryIcepickVersion"
                }
            }
        }
        "com.squareup.retrofit2:retrofit" { //Retrofit
            gradle {
                dependencies {
                    compile "com.squareup.retrofit2:retrofit:$libraryRetrofitVersion"
                    compile "com.squareup.retrofit2:converter-gson:$libraryRetrofitVersion"
                    compile "com.squareup.okhttp3:logging-interceptor:$libraryLoggingInterceptorVersion"
                }
            }
        }
        "com.google.android.gms:play-services-vision" { //PandroidScannerView
            gradle {
                dependencies {
                    compile "com.google.android.gms:play-services-vision:$libraryGmsVersion"
                }
            }
            manifest {
                '''
     <application>
        <meta-data
            android:name="com.google.android.gms.version"
            android:value="@integer/google_play_services_version" />
        <meta-data
            android:name="com.google.android.gms.vision.DEPENDENCIES"
            android:value="barcode,face" />
     </application>
'''
            }
        }

        "com.google.firebase:firebase-core" {
            /*
Add this part in your build.gradle
//tag::FirebaseAnalytics[]
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.gms:google-services:3.0.0'
    }
}
//apply the plugin at the end of your build.gradle to avoid error
apply plugin 'com.google.gms.google-services'
//end::FirebaseAnalytics[]
*/
            gradle {
                dependencies {
                    compile "com.google.firebase:firebase-analytics:$libraryGmsVersion"
                }
            }
        }
        "com.google.android.gms:play-services-analytics" { //GoogleAnalyticsManager
            gradle {
                /*
Add this part in your build.gradle
//tag::GoogleAnalytics[]
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.gms:google-services:3.0.0'
    }
}
//apply the plugin at the end of your build.gradle to avoid error
apply plugin 'com.google.gms.google-services'
    //end::GoogleAnalytics[]
                */

                dependencies {
                    compile "com.google.android.gms:play-services-analytics:$libraryGmsVersion"
                }
            }

            manifest {
                '''
    <application>
        <meta-data
            android:name="com.google.android.gms.version"
            android:value="@integer/google_play_services_version" />
        <receiver
            android:name="com.google.android.gms.analytics.AnalyticsReceiver"
            android:enabled="true">
            <intent-filter>
                <action android:name="com.google.android.gms.analytics.ANALYTICS_DISPATCH" />
            </intent-filter>
        </receiver>
        <service
            android:name="com.google.android.gms.analytics.AnalyticsService"
            android:enabled="true"
            android:exported="false" />
     </application>
'''
            }
        }
        "com.github.johnpersano:supertoasts" { //SuperToastManagerImpl
            gradle {
                dependencies {
                    compile("com.github.johnpersano:supertoasts:$librarySupertoastsVersion@aar") {
                        transitive = true
                        exclude group: 'com.android.support', module: 'support-annotations'
                    }
                }
            }
        }
        "com.github.bumptech.glide:glide" { //GlidePictureManagerImpl
            gradle {
                dependencies {
                    compile "com.github.bumptech.glide:glide:$libraryGlideVersion"
                    compile "com.github.bumptech.glide:annotations:$libraryGlideVersion"
                    compile "com.github.bumptech.glide:okhttp3-integration:$libraryGlideVersion"
                    annotationProcessor "com.github.bumptech.glide:compiler:$libraryGlideVersion"
                }
            }
        }
        "com.crashlytics.sdk.android:crashlytics" { //CrashlyticsLogger
            gradle {
                /*
    Add this part in your build.gradle
    //tag::Crashlytics[]
    buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.4'
    }
    }
    //end::Crashlytics[]

                */
                repositories {
                    maven { url "https://maven.fabric.io/public" }
                }
                dependencies {
                    compile("com.crashlytics.sdk.android:crashlytics:$libraryCrashlyticsVersion@aar") {
                        transitive true
                    }
                }

                if (!plugins.hasPlugin("io.fabric")) {
                    apply plugin: "io.fabric"
                } else if (plugins.findPlugin("io.fabric") == null) {
                    throw new GradleException("crashlytics plugin is missing. please add classpath 'io.fabric.tools:gradle:1.24.4'")
                }

            }
            manifest {
                config ->
                    if (config.apiKey) {
                        return '''
    <application>
        <meta-data
            android:name="io.fabric.ApiKey"
            android:value="''' + config.apiKey + '''" />
     </application>
'''
                    }
                    return ""
            }
        }

    }

}