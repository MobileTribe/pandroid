apply plugin: 'com.android.library'
apply from: 'dagger.gradle'

apply from: file('../delivery.gradle')

android {
    compileSdkVersion Integer.parseInt(androidSdkVersion)
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        minSdkVersion Integer.parseInt(androidMinSdkVersion)
        targetSdkVersion Integer.parseInt(androidSdkVersion)
        versionCode Integer.parseInt(pandroidVersionId)
        versionName pandroidVersion
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true // FIXME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), './pandroid-proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }

}



dependencies {

    //Core
    compile project(':pandroid-java')
    annotationProcessor project(':pandroid-compiler')
    provided project(':pandroid-annotations')
    compile "com.android.support:support-core-ui:$librarySupportVersion"
    compile "com.android.support:appcompat-v7:$librarySupportVersion"
    compile "com.android.support:cardview-v7:$librarySupportVersion"
    compile "com.android.support:gridlayout-v7:$librarySupportVersion"
    compile "com.android.support:palette-v7:$librarySupportVersion"
    compile "com.android.support:recyclerview-v7:$librarySupportVersion"
    compile "com.android.support:support-v13:$librarySupportVersion"
    compile "com.android.support:design:$librarySupportVersion"


    //Test
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.1.4"


    //Support to simulate plugin work
    //Optional library have to be declared in pandroid.gradle
    optionalLibs.each{
        libDep ->
            provided(libDep){
                transitive = true;
            }
    }
    provided "frankiesardo:icepick-processor:$libraryIcepickVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$libraryButterknifeVersion"

    provided 'org.jetbrains:annotations-java5:15.0'
}


//configure template with delivery

def zipTemplatesTask = project(":pandroid-demo").getTasksByName("zipTemplates", false)
task buildTemplate(type: DeliveryBuild, dependsOn: zipTemplatesTask) {
    variantName = project.name
    outputFiles = ["templates": zipTemplatesTask.outputs.files.singleFile.first()]
}
