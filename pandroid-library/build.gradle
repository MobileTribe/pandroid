apply plugin: 'com.android.library'
apply from: 'dagger.gradle'

apply from: file('../delivery.gradle')

android {
    compileSdkVersion Integer.parseInt(androidSdkVersion)
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        minSdkVersion Integer.parseInt(androidMinSdkVersion)
        targetSdkVersion Integer.parseInt(androidSdkVersion)
        versionCode Integer.parseInt(pandroidVersionId)
        versionName pandroidVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), './pandroid-proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        disable 'MissingPermission'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}



dependencies {

    //Core
    api project(':pandroid-java')
    annotationProcessor project(':pandroid-compiler')
    compileOnly project(':pandroid-annotations')
    api "com.android.support:support-annotations:$librarySupportVersion"
    api "com.android.support:support-core-ui:$librarySupportVersion"
    api "com.android.support:appcompat-v7:$librarySupportVersion"
    api "com.android.support:cardview-v7:$librarySupportVersion"
    api "com.android.support:gridlayout-v7:$librarySupportVersion"
    api "com.android.support:palette-v7:$librarySupportVersion"
    api "com.android.support:recyclerview-v7:$librarySupportVersion"
    api "com.android.support:support-v13:$librarySupportVersion"
    api "com.android.support:design:$librarySupportVersion"

    //Test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.5.1"

    testImplementation "io.reactivex.rxjava2:rxandroid:$libraryRxAndroidVersion"
    testImplementation "io.reactivex.rxjava2:rxjava:$libraryRxJavaVersion"

    //Support to simulate plugin work
    //Optional library have to be declared in pandroid.gradle
    optionalLibs.each {
        libDep ->
            compileOnly(libDep) {
                transitive = true;
            }
    }
    annotationProcessor "frankiesardo:icepick-processor:$libraryIcepickVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$libraryButterknifeVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$libraryGlideVersion"

    compileOnly 'org.jetbrains:annotations-java5:15.0'
}

//configure template with delivery

def zipTemplatesTask = project(":pandroid-demo").getTasksByName("zipTemplates", false)
task buildTemplate(type: DeliveryBuild, dependsOn: zipTemplatesTask) {
    variantName = project.name
    outputFiles = ["templates": zipTemplatesTask.outputs.files.singleFile.first()]
}
