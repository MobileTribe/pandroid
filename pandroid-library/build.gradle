apply plugin: 'com.android.library'
apply from: 'dagger.gradle'

apply from: file('../delivery.gradle')

android {
    compileSdkVersion Integer.parseInt(androidSdkVersion)

    defaultConfig {
        minSdkVersion Integer.parseInt(androidMinSdkVersion)
        targetSdkVersion Integer.parseInt(androidSdkVersion)
        versionCode Integer.parseInt(pandroidVersionId)
        versionName pandroidVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), './pandroid-proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        disable 'MissingPermission'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }


}



dependencies {

    //Core
    api project(':pandroid-java')
    annotationProcessor project(':pandroid-compiler')
    compileOnly project(':pandroid-annotations')
    api "androidx.annotation:annotation:1.0.0"
    api "androidx.appcompat:appcompat:1.0.0"
    api "androidx.cardview:cardview:1.0.0"
    api "androidx.gridlayout:gridlayout:1.0.0"
    api "androidx.palette:palette:1.0.0"
    api "androidx.recyclerview:recyclerview:1.0.0"
    api "com.google.android.material:material:1.0.0"

    //Test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.8"

    testImplementation "io.reactivex.rxjava2:rxandroid:$libraryRxAndroidVersion"
    testImplementation "io.reactivex.rxjava2:rxjava:$libraryRxJavaVersion"

    //Support to simulate plugin work
    //Optional library have to be declared in pandroid.gradle
    optionalLibs.each {
        libDep ->
            compileOnly(libDep) {
                transitive = true;
            }
    }
    annotationProcessor "frankiesardo:icepick-processor:$libraryIcepickVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$libraryButterknifeVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$libraryGlideVersion"

    compileOnly 'org.jetbrains:annotations-java5:15.0'
}

//configure template with delivery

def zipTemplatesTask = project(":pandroid-demo").getTasksByName("zipTemplates", false)
task buildTemplate(type: DeliveryBuild, dependsOn: zipTemplatesTask) {
    variantName = project.name
    outputFiles = ["templates": zipTemplatesTask.outputs.files.singleFile.first()]
}
